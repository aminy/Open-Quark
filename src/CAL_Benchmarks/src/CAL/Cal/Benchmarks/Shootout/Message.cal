/*
 * Copyright (c) 2007 BUSINESS OBJECTS SOFTWARE LIMITED
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     * Redistributions of source code must retain the above copyright notice,
 *       this list of conditions and the following disclaimer.
 *
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *
 *     * Neither the name of Business Objects nor the names of its contributors
 *       may be used to endorse or promote products derived from this software
 *       without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

/**
 * Messages benchmark implementation for the internet bechmark shootouts see: {@url
 * http://shootout.alioth.debian.org@}
 *
 * This version of the benchmark uses the {@link typeConstructor =
 * BlockingQueue@} to pass messages between workers. There is a second version
 * of thsi benchmark, {@link module = "Cal.Benchmarks.Shootout.Message_2"@}
 * which is based on mutable variables.
 *
 * This code is based on and inspired by the entries in the The Great Computer
 * Language Shootout {@url http://shootout.alioth.debian.org/@}.
 * @author Magnus Byne
 */
module Cal.Benchmarks.Shootout.Message;

import Cal.Core.Prelude using
    typeConstructor = Int, String;
    function = eager, error, intToString, seq, stringToInt;
    ;

import Cal.IO.Console using
    function = printLine;
    ;

import Cal.Collections.List using
    function = head;
    ;

import Cal.Experimental.Concurrent.Parallel using
    function = par;
    ;

import Cal.Experimental.Concurrent.BlockingQueue using
    typeConstructor = BlockingQueue;
    function = makeBlockingQueue, take, put;
    ;

import Cal.Core.System using
    function = concurrentRuntime;
    ;

/** this value is used to tell the workers to end */
end :: Int;
end = -1;

/** default queue length */
queueLength = 256;

/**
 * an indivual worker which takes values from the upstream queue, adds one, and
 * puts them on the downstream queue.
 */
worker :: BlockingQueue Int -> BlockingQueue Int -> ();
worker !inq !outq =
    let
        input :: Int;
        input = eager $ take inq;
    in
        if (input == end) then
            put outq end
            `seq`
            ()
        else
            (
                put outq (input + 1)
                `seq`
                worker inq outq
            )
    ;

/**
 * create a chain of worker functions running in parallel this is done using
 * the par function.
 */
createChain :: Int -> BlockingQueue Int -> BlockingQueue Int;
createChain !length !input =
    if (length == 0) then
        input
    else
        (
            let
                output :: BlockingQueue Int;
                output = makeBlockingQueue queueLength;
            in
                worker input output
                `par`
                createChain (length - (1 :: Int)) output
        )
    ;

main :: [String] -> ();
public main !args =
    let
        n :: Int;
        n = eager $ stringToInt (head args);

        inQueue :: BlockingQueue Int;
        inQueue = eager $ makeBlockingQueue queueLength;

        outQueue :: BlockingQueue Int;
        outQueue = eager $ createChain (500 :: Int) inQueue;

        /** this feeds n 0's into the first queue */
        feedQueue :: Int -> ();
        feedQueue n =
            if (n == 0) then
                put inQueue end
            else
                put inQueue (0 :: Int)
                `seq`
                feedQueue (n - 1)
            ;

        /** this collects n values from the last queue */
        sumResult :: Int -> Int -> Int;
        sumResult !n !sum =
            if (n == 0) then
                sum
            else
                sumResult (n - 1) (sum + (take outQueue))
            ;
    in
        if concurrentRuntime then
            feedQueue n 
            `par` 
            printLine (intToString $ sumResult n 0)
        else
            error
                "You must define the VM arg org.openquark.cal.machine.lecc.concurrent_runtime to run this program"
    ;
